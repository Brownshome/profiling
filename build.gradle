plugins {
	id 'java-library'
	id 'maven-publish'
}

group = 'brownshome'
version = '1.0.0'

repositories {
	mavenCentral()
}

java {
	withJavadocJar()
	withSourcesJar()
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

publishing {
	repositories {
		maven {
			name = 'GitHubPackages'
			url = uri('https://maven.pkg.github.com/Brownshome/profiling')
			credentials {
				println project.getProperties()
				username = project.properties['gpr.usr']
				password = project.properties['gpr.key']
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			artifactId = 'profiling'
			from components.java

			pom {
				name = 'Profiling'
				description = 'A small library for investigating runtime performance'
				url = 'https://github.com/Brownshome/profiling'
				developers {
					developer {
						id = 'Brownshome'
						name = 'James Brown'
						email = 'jamesphone180@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/Brownshome/profiling.git'
					url = 'https://github.com/Brownshome/profiling'
				}
			}
		}
	}
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-api:[5.0.0,6.0.0)'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:[5.0.0,6.0.0)'
}

test {
	useJUnitPlatform()
}